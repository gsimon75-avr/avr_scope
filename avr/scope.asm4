; Expected symbols (via -Dname=value in commandline)
; F_CPU cpu freq in Hz

include(boilerplate.m4)
include(iosyms.m4)

define({RECV_BUF_SIZE}, 4)
define({NUM_SAMPLES}, 800)
define({USE_BAUD}, BAUDVALUE(250000))
define({TRIG_HIST}, 4)
define({TRIG_NONE}, 0)
define({TRIG_RISING_ANALOG}, 1)
define({TRIG_FALLING_ANALOG}, 2)
define({TRIG_RISING_DIGITAL}, 3)
define({TRIG_FALLING_DIGITAL}, 4)

define({OP_SCOPE}, 0)
define({OP_ZCD}, 1)
define({OP_FULLSPEED_SCOPE}, 2)

; flags in GPIOR0
define({SEND_COMPLETE}, 0)

; port/pin assignment    
define({D_PWM}, 3)
define({D_LED}, 2)
define({C_IN_DC}, 0)
define({C_IN_AC}, 1)
define({DIGITAL_TRIGGER}, 0x80)

; register mapping
define({trig_low}, r9)
define({trig_high}, r10)
define({trig_level}, r11)
define({usart_sreg_tmp}, r12)
define({adc_sreg_tmp}, r14)
; generic working registers: r16, r17
define({op_mode}, r21)
define({admux}, r22)
define({delay_counter_max}, r23)
define({cmd_start_adc}, r24)
define({trig_type}, r25)
; sample_ptr X[H,L] address of the next sample: r26, r27
; recv_ptr Y[H,L], used by recvd_push and recvd_pop: r28, r29

define({BAUDVALUE}, {eval((F_CPU / (8*($1))) - 1)})

define({nop4}, nop2()
    nop2())

define({nop8}, nop4()
    nop4())

define({nop16}, nop8()
    nop8())

define({nop32}, nop16()
    nop16())

; ------------------------------------------------------------------------------
.CSEG
.org 0
jmp(main)

rcall(trap)
nop()

rcall(trap)
nop()

rcall(trap)
nop()

rcall(trap)
nop()

rcall(trap)
nop()

rcall(trap)
nop()

rcall(trap)
nop()

rcall(trap)
nop()

rcall(trap)
nop()

rcall(trap)
nop()

rcall(trap)
nop()

rcall(trap)
nop

rcall(trap)
nop()

rcall(trap)
nop()

rcall(trap)
nop()

rcall(trap)
nop()

rcall(trap)
nop()

rjmp(USART_RX_vect)
nop

rjmp(USART_UDRE_vect)
nop()

rcall(trap)
nop()

rcall(trap)
nop

rcall(trap)
nop()

rcall(trap)
nop()

rcall(trap)
nop()

rcall(trap)
nop()

FUNCTION(trap)
    cli
    ldi(r16, 1)
    rcall(led_dot)
  LOC(q):
    rjmp(LOC(q))
ENDFUNC

FUNCTION(led_dot)
    ; In: r16 = onoff
    ; Out: none
    ; Changes: SREG
    cpi(r16, 0)
    breq(LOC(OFF))
    sbi(PORTD, D_LED)
    ret()
  LOC(OFF):
    cbi(PORTD, D_LED)
    ret()
ENDFUNC ; led_dot


FUNCTION(led_toggle)
    ; In: none
    ; Out: none
    ; Changes: SREG
    sbic(PORTD, D_LED)
    rjmp(LOC(OFF))
    nop()
    sbi(PORTD, D_LED)
    ret()
  LOC(OFF):
    cbi(PORTD, D_LED)
    ret()
ENDFUNC ; led_toggle


FUNCTION(recvd_push)
    ; In: r16 = x
    ; Out: None
    ; Changes: Y
    ;
    ; check wrap -> store -> increment
    cpi(YH, (recv_buf + RECV_BUF_SIZE) >> 8)
    brlo(LOC(NO_WRAP_AROUND))
    cpi(YL, (recv_buf + RECV_BUF_SIZE) & 0xff)
    brlo(LOC(NO_WRAP_AROUND))
    ldi(YL, recv_buf & 0xff)
    ldi(YH, recv_buf >> 8)
  LOC(NO_WRAP_AROUND):
    st(Y+, r16)
    ret()
ENDFUNC ; recvd_push


FUNCTION(recvd_pop)
    ; In: none
    ; Out: r16 = x
    ; Changes: Y
    ;
    ; decrement -> load -> check wrap
    ld(r16, -Y)
    cpi(YH, recv_buf >> 8)
    brlo(LOC(WRAP_AROUND))
    brne(LOC(NO_WRAP_AROUND))
    cpi(YL, recv_buf & 0xff)
    brsh(LOC(NO_WRAP_AROUND))
  LOC(WRAP_AROUND):
    ldi(YL, (recv_buf + RECV_BUF_SIZE) & 0xff)
    ldi(YH, (recv_buf + RECV_BUF_SIZE) >> 8)
  LOC(NO_WRAP_AROUND):
    ret()
ENDFUNC ; recvd_pop


FUNCTION(recvd_pop_byte)
    ; In: none
    ; Out: r16 = x
    ; Changes: Y, r17
    rcall(recvd_pop)
    mov(r17, r16)
    rcall(recvd_pop)
    lsl(r16)
    lsl(r16)
    lsl(r16)
    lsl(r16)
    or(r16, r17)
    ret()
ENDFUNC ; recvd_pop_byte

FUNCTION(set_sample_rate)
    ; In: r16
    ; Delay = 16 + 8*N clks -> N = delay/8 - 2
    ; 0:   2 us =   32 clks, N =   2
    ; 1:   5 us =   80 clks, N =   8
    ; 2:  10 us =  160 clks, N =  18
    ; 3:  20 us =  320 clks, N =  38
    ; 4:  50 us =  800 clks, N =  98
    ; 5: 100 us = 1600 clks, N = 198
    ldi(cmd_start_adc, _BV(ADEN, ADSC) | 2)
    ldi(delay_counter_max, 2)
    tst(r16)
    breq(LOC(DONE))
    ldi(cmd_start_adc, _BV(ADEN, ADSC) | 2)
    ldi(delay_counter_max, 8)
    dec(r16)
    breq(LOC(DONE))
    ldi(cmd_start_adc, _BV(ADEN, ADSC) | 3)
    ldi(delay_counter_max, 18)
    dec(r16)
    breq(LOC(DONE))
    ldi(cmd_start_adc, _BV(ADEN, ADSC) | 4)
    ldi(delay_counter_max, 38)
    dec(r16)
    breq(LOC(DONE))
    ldi(cmd_start_adc, _BV(ADEN, ADSC) | 5)
    ldi(delay_counter_max, 98)
    dec(r16)
    breq(LOC(DONE))
    ldi(cmd_start_adc, _BV(ADEN, ADSC) | 6)
    ldi(delay_counter_max, 198)
  LOC(DONE):
    mov(r16, delay_counter_max)
    ret()
ENDFUNC ; set_sample_rate


FUNCTION(set_vref)
    ; In: r16
    andi(admux, 0xff - _BV(REFS1))
    sbrs(r16, 0)
    ori(admux, _BV(REFS1)) ; ref=AVcc
    sts(ADMUX, admux)
    set() ; re-check trig
    ret()
ENDFUNC ; set_vref


FUNCTION(set_zero_mode)
    ; In: r16
    cbr(admux, 0x0f)
    andi(r16, 0x01)
    or(admux, r16)
    sts(ADMUX, admux)
    set() ; re-check trig
    ret()
ENDFUNC ; set_zero_mode


FUNCTION(set_op_mode)
    ; In: r16
    andi(r16, 0x03)
    mov(op_mode, r16)

    cpi(r16, OP_ZCD)
    breq(LOC(MODE_ZCD))
    ret()

  LOC(MODE_ZCD):
    clr(r16)
    sts(ADCSRA, r16)
    ret()
ENDFUNC ; set_op_mode


FUNCTION(set_trig_type)
    ; In: r16
    andi(r16, 0x07)
    mov(trig_type, r16)
    set() ; re-check trig
    ret()
ENDFUNC ; set_trig_type


FUNCTION(set_trig_level)
    ; In: r16
    mov(trig_level, r16)
    mov(r17, r16)
    addi(r16, TRIG_HIST)
    brcs(LOC(TRIG_LEVEL_NOT_TOO_HIGH))
    ldi(r16, 0xff - TRIG_HIST)
  LOC(TRIG_LEVEL_NOT_TOO_HIGH):
    mov(trig_high, r16)
    subi(r17, TRIG_HIST)
    brcc(LOC(TRIG_LEVEL_NOT_TOO_LOW))
    ldi(r17, TRIG_HIST)
  LOC(TRIG_LEVEL_NOT_TOO_LOW):
    mov(trig_low, r17)
    set() ; re-check trig
    ret()
ENDFUNC ; set_trig_level


FUNCTION(set_pwm_prescaler)
    ; In: r16
    andi(r16, 0x07)
    ori(r16, _BV(WGM22))
    sts(TCCR2B, r16)
    ret()
ENDFUNC ; set_pwm_prescaler


;FUNCTION(set_)
;    ; In: r16
;    ret()
;ENDFUNC ; set_


FUNCTION(USART_RX_vect)
    in(usart_sreg_tmp, SREG)
    push(r16)
    push(r17)

    lds(r16, UDR0)

    cpi(r16, 'S')
    breq(LOC(SET_SPEED))
    cpi(r16, 'R')
    breq(LOC(SET_VREF))
    cpi(r16, 'Z')
    breq(LOC(SET_ZERO))
    cpi(r16, 'T')
    breq(LOC(SET_TRIG_TYPE))
    cpi(r16, 'L')
    breq(LOC(SET_TRIG_LEVEL))
    cpi(r16, 'M')
    breq(LOC(SET_MODE))

    cpi(r16, 't')
    breq(LOC(SET_PWM_TOTAL))
    cpi(r16, 'h')
    breq(LOC(SET_PWM_HIGH))
    cpi(r16, 'p')
    breq(LOC(SET_PWM_PRESCALER))

    cpi(r16, '0')
    brlt(LOC(NOT_DIGIT))
    cpi(r16, '9' + 1)
    brge(LOC(NOT_09))
    subi(r16, '0')
    rjmp(LOC(DIGIT))
  LOC(NOT_09):
    cpi(r16, 'A')
    brlt(LOC(NOT_DIGIT))
    cpi(r16, 'F' + 1)
    brge(LOC(NOT_AF))
    subi(r16, 'A' - 10)
    rjmp(LOC(DIGIT))
  LOC(NOT_AF):
    cpi(r16, 'a')
    brlt(LOC(NOT_DIGIT))
    cpi(r16, 'f' + 1)
    brge(LOC(NOT_DIGIT))
    subi(r16, 'a' - 10)
  LOC(DIGIT):
    rcall(recvd_push)
  LOC(NOT_DIGIT):
    rjmp(LOC(DONE))

  LOC(SET_SPEED):
    rcall(recvd_pop)
    rcall(set_sample_rate)
    rjmp(LOC(DONE))

  LOC(SET_VREF):
    rcall(recvd_pop)
    rcall(set_vref)
    rjmp(LOC(DONE))

  LOC(SET_ZERO):
    rcall(recvd_pop)
    rcall(set_zero_mode)
    rjmp(LOC(DONE))

  LOC(SET_TRIG_TYPE):
    rcall(recvd_pop)
    rcall(set_trig_type)
    rjmp(LOC(DONE))

  LOC(SET_TRIG_LEVEL):
    rcall(recvd_pop_byte)
    rcall(set_trig_level)
    rjmp(LOC(DONE))

  LOC(SET_MODE):
    rcall(recvd_pop_byte)
    rcall(set_op_mode)
    rjmp(LOC(DONE))

  LOC(SET_PWM_TOTAL):
    rcall(recvd_pop_byte)
    sts(OCR2A, r16) ; pwm wave length
    rjmp(LOC(DONE))

  LOC(SET_PWM_HIGH):
    rcall(recvd_pop_byte)
    sts(OCR2B, r16) ; pwm wave high
    rjmp(LOC(DONE))

  LOC(SET_PWM_PRESCALER):
    rcall(recvd_pop)
    rcall(set_pwm_prescaler)
    rjmp(LOC(DONE))

  LOC(DONE):
    pop(r17)
    pop(r16)
    brts(LOC(PRESERVE_T))
    out(SREG, usart_sreg_tmp)
    reti()
  LOC(PRESERVE_T):
    out(SREG, usart_sreg_tmp)
    set()
    reti()
ENDFUNC ; USART_RX_vect

FUNCTION(USART_UDRE_vect)
    sbic(GPIOR0, SEND_COMPLETE)
    reti()
    in(usart_sreg_tmp, SREG)
    cpi(XH, end_of_samples >> 8)
    brlo(LOC(NEXT_SAMPLE))
    cpi(XL, end_of_samples & 0xff)
    breq(LOC(LAST_SAMPLE))
    brlo(LOC(NEXT_SAMPLE))
    out(SREG, usart_sreg_tmp)
    sbi(GPIOR0, SEND_COMPLETE)
    reti()

  LOC(LAST_SAMPLE):
    adiw(XL, 1)
    ldi(r16, 0xff)
    rjmp(LOC(NOT_MAX))
    
  LOC(NEXT_SAMPLE):
    ld(r16, X+)
    cpi(r16, 0xff)
    brne(LOC(NOT_MAX))
    dec(r16)
  LOC(NOT_MAX):
    sts(UDR0, r16)
    out(SREG, usart_sreg_tmp)
    reti()
ENDFUNC ; USART_UDRE_vect

FUNCTION(read_adc)
    ; In: None
    ; Out: r16
    sts(ADCSRA, cmd_start_adc)
  LOC(WAIT_COMPLETE):
    lds(r16, ADCSRA)
    andi(r16, _BV(ADSC))
    brne(LOC(WAIT_COMPLETE))
    lds(r16, ADCH)
    ret
ENDFUNC ; read_adc


FUNCTION(wait_trig)
  LOC(CHECK_TRIG):
    clt() ; serial data receiver sets T to signal re-evaluating the trig conditions
    cpi(trig_type, TRIG_RISING_ANALOG)
    breq(LOC(WAIT_TRIG_RISING_ANALOG))
    cpi(trig_type, TRIG_FALLING_ANALOG)
    breq(LOC(WAIT_TRIG_FALLING_ANALOG))
    cpi(trig_type, TRIG_RISING_DIGITAL)
    breq(LOC(WAIT_TRIG_RISING_DIGITAL))
    cpi(trig_type, TRIG_FALLING_DIGITAL)
    breq(LOC(WAIT_TRIG_FALLING_DIGITAL))
  LOC(START_SAMPLING):
    ret

  LOC(WAIT_TRIG_FALLING_ANALOG): ; wait for trig pre-condition
    brts(LOC(CHECK_TRIG))
    rcall(read_adc)
    cp(r16, trig_high)
    brlo(LOC(WAIT_TRIG_FALLING_ANALOG))
  LOC(WAIT_TRIG_FALLING_2_ANALOG): ; wait for trig condition
    brts(LOC(CHECK_TRIG))
    ; rcall(read_adc) ; inlined below to reclaim 3+4 clks of `rcall + ret`
    sts(ADCSRA, cmd_start_adc)
  LOC(WAIT_COMPLETE_F):
    lds(r16, ADCSRA)
    andi(r16, _BV(ADSC))
    brne(LOC(WAIT_COMPLETE_F))
    lds(r16, ADCH)
    cp(r16, trig_low)
    brlo(LOC(START_SAMPLING))
    rjmp(LOC(WAIT_TRIG_FALLING_2_ANALOG))

  LOC(WAIT_TRIG_RISING_ANALOG): ; wait for trig pre-condition
    brts(LOC(CHECK_TRIG))
    rcall(read_adc)
    cp(r16, trig_low)
    brsh(LOC(WAIT_TRIG_RISING_ANALOG))
  LOC(WAIT_TRIG_RISING_2_ANALOG): ; wait for trig condition
    brts(LOC(CHECK_TRIG))
    ;rcall(read_adc) ; inlined below to reclaim 3+4 clks of `rcall + ret`
    sts(ADCSRA, cmd_start_adc)
  LOC(WAIT_COMPLETE_R):
    lds(r16, ADCSRA)
    andi(r16, _BV(ADSC))
    brne(LOC(WAIT_COMPLETE_R))
    lds(r16, ADCH)
    cp(r16, trig_high)
    brsh(LOC(START_SAMPLING))
    rjmp(LOC(WAIT_TRIG_RISING_2_ANALOG))

  LOC(WAIT_TRIG_FALLING_DIGITAL): ; wait for trig pre-condition
    brts(LOC(CHECK_TRIG))
    in(r16, PIND)
    andi(r16, DIGITAL_TRIGGER)
    breq(LOC(WAIT_TRIG_FALLING_DIGITAL))
  LOC(WAIT_TRIG_FALLING_2_DIGITAL): ; wait for trig condition
    brts(LOC(CHECK_TRIG))
    in(r16, PIND)
    andi(r16, DIGITAL_TRIGGER)
    breq(LOC(START_SAMPLING))
    rjmp(LOC(WAIT_TRIG_FALLING_2_DIGITAL))

  LOC(WAIT_TRIG_RISING_DIGITAL): ; wait for trig pre-condition
    brts(LOC(CHECK_TRIG))
    in(r16, PIND)
    andi(r16, DIGITAL_TRIGGER)
    brne(LOC(WAIT_TRIG_RISING_DIGITAL))
  LOC(WAIT_TRIG_RISING_2_DIGITAL): ; wait for trig condition
    brts(LOC(CHECK_TRIG))
    in(r16, PIND)
    andi(r16, DIGITAL_TRIGGER)
    brne(LOC(START_SAMPLING))
    rjmp(LOC(WAIT_TRIG_RISING_2_DIGITAL))
ENDFUNC ; wait_trig


FUNCTION(sampler_analog_scope)
    ; Read the next sweep of samples from ADC
    ldi(r16, NUM_SAMPLES / 4)
    mov(r2, r16)

    rcall(wait_trig)

    ; The point here is to create a delay loop with a constant and a repeatable part
    ; whose sum is a divisor of 16 clks and can express 1600 at top.
    ; I don't want to unroll 1600 `nop`s, at least not until all more civil approaches
    ; have been exhausted, so it'll be a loop with a counter.
    ;
    ; The constant part has to do the actual work (read and store the ADC value,
    ; increment the dest pointer and check for end-of-sweep), so it's at least
    ; 2+2+2+1 + 1+1+(2+2 | 1+1+2) = 13 clks, so when expressing 16, we have two approaches:
    ;
    ; 1. The loop runs once, and the repeating part can be at most 3 clks, which is ok for a `dec; brnz`
    ; 2. The loop runs 0 times, the constant part takes up 16, but this requires a forward-testing loop
    ;
    ; As of approach #1, if the constant part is 13 and the repeatable 3, it's fine for 16, but
    ; even for 32 the loop should run (32-13)/3 = 6 1/3 times, so this option is out.
    ;
    ; Approach #2 is it then, so the constant part is 16, the loop must be a forward-testing
    ; one, the repeatable length must divide 16 clks, and must be at least (1600-16)/255 <= 7
    ; clks long. (At least if I want the counter keep within 8 bits, which I'd prefer to.)
    ;
    ; So the repeatable part is either 8 or 16 clks. Even for a forward-test and jump-back 8
    ; seems fairly enough, so this is the time for some constructive laziness. No point in
    ; optimising a delay loop, remember ;) ?

    ; The loop with the delay code is 2+2+2+(3)+1 + (1+1+1+1+1+1+2)*N + 1+1+(2+2 | 1+1+2) = 16 + 8*N clks
    ; That makes N = clks/8 - 2
    ;
    ; 100 us = 1600 clks, N = 198
    ;  50 us =  800 clks, N =  98
    ;  20 us =  320 clks, N =  38
    ;  10 us =  160 clks, N =  18
    ;   5 us =   80 clks, N =   8
    ;   2 us =   32 clks, N =   2
    
  LOC(START_SAMPLING):
    cli()
  LOC(NEXT_SAMPLE):

    lds(r16, ADCH) ; 2 clk
    sts(ADCSRA, cmd_start_adc) ; 2 clk
    st(X+, r16) ; 2 clk
    in(r16, PIND) ; 1 clk
    st(X+, r16) ; 2 clk
    mov(r16, delay_counter_max) ; 1 clk
  LOC(WAIT_LOOP_0):
    tst(r16); 1 clk
    breq(LOC(WAIT_DONE_0)) ; calculate as 1 clk, add the 2nd one (if jump taken) after the loop
    dec(r16) ; 1 clk
    nop2(); 2 clks
    nop() ; 1 clk
    rjmp(LOC(WAIT_LOOP_0)) ; 2 clks
  LOC(WAIT_DONE_0):
    ; +1 clk, because the `breq` above takes 2 when it actually jumps here
    nop2(); 2 clks
    nop() ; 1 clk

    lds(r16, ADCH) ; 2 clk
    sts(ADCSRA, cmd_start_adc) ; 2 clk
    st(X+, r16) ; 2 clk
    in(r16, PIND) ; 1 clk
    st(X+, r16) ; 2 clk
    mov(r16, delay_counter_max) ; 1 clk
  LOC(WAIT_LOOP_1):
    tst(r16); 1 clk
    breq(LOC(WAIT_DONE_1)) ; calculate as 1 clk, add the 2nd one (if jump taken) after the loop
    dec(r16) ; 1 clk
    nop2(); 2 clks
    nop() ; 1 clk
    rjmp(LOC(WAIT_LOOP_1)) ; 2 clks
  LOC(WAIT_DONE_1):
    ; +1 clk, because the `breq` above takes 2 when it actually jumps here
    nop2(); 2 clks
    nop() ; 1 clk

    lds(r16, ADCH) ; 2 clk
    sts(ADCSRA, cmd_start_adc) ; 2 clk
    st(X+, r16) ; 2 clk
    in(r16, PIND) ; 1 clk
    st(X+, r16) ; 2 clk
    mov(r16, delay_counter_max) ; 1 clk
  LOC(WAIT_LOOP_2):
    tst(r16); 1 clk
    breq(LOC(WAIT_DONE_2)) ; calculate as 1 clk, add the 2nd one (if jump taken) after the loop
    dec(r16) ; 1 clk
    nop2(); 2 clks
    nop() ; 1 clk
    rjmp(LOC(WAIT_LOOP_2)) ; 2 clks
  LOC(WAIT_DONE_2):
    ; +1 clk, because the `breq` above takes 2 when it actually jumps here
    nop2(); 2 clks
    nop() ; 1 clk

    lds(r16, ADCH) ; 2 clk
    sts(ADCSRA, cmd_start_adc) ; 2 clk
    st(X+, r16) ; 2 clk
    in(r16, PIND) ; 1 clk
    st(X+, r16) ; 2 clk
    mov(r16, delay_counter_max) ; 1 clk
  LOC(WAIT_LOOP_3):
    tst(r16); 1 clk
    breq(LOC(WAIT_DONE_3)) ; calculate as 1 clk, add the 2nd one (if jump taken) after the loop
    dec(r16) ; 1 clk
    nop2(); 2 clks
    nop() ; 1 clk
    rjmp(LOC(WAIT_LOOP_3)) ; 2 clks
  LOC(WAIT_DONE_3):
    ; +1 clk, because the `breq` above takes 2 when it actually jumps here

    dec(r2) ; 1 clk
    brne(LOC(NEXT_SAMPLE)) ; 2 clks

    ret() ; 4 clks
ENDFUNC ; sampler_analog_scope


FUNCTION(sampler_zcd)
    ; Read the next sweep of samples from the comparator input
    sei()
    ; process any pending interrupts here
    ;ldi(r16, 3) ; 3=prescaler 1:64
    ;ldi(r16, 2) ; 2=prescaler 1:8
    ldi(r16, 1) ; 1=prescaler 1:1
    mov(r0, r16) ; TCCR1B for falling edge capture
    ori(r16, _BV(ICES1))
    mov(r1, r16) ; TCCR1B for rising edge capture
    clr(r16)
    cli()
    sts(TCNT1H, r16)
    sts(TCNT1L, r16)

  LOC(NEXT_SAMPLE):

    sts(TCCR1B, r0) ; 2 clks
    sbi(TIFR1, ICF1) ; 2 clks
  LOC(WAIT_FALLING):
    sbis(TIFR1, ICF1) ; 2 clks if skip, 1 clk if continue -> calculate with 1 clk
    rjmp(LOC(WAIT_FALLING)) ; 2 clks
    ; skipping of `sbis` above: 1 clk
    lds(r16, ICR1L) ; 2 clks
    st(X+, r16) ; 2 clks
    in(r16, PIND) ; 1 clk
    st(X+, r16) ; 2 clk

    sts(TCCR1B, r1)
    sbi(TIFR1, ICF1)
  LOC(WAIT_RISING):
    sbis(TIFR1, ICF1)
    rjmp(LOC(WAIT_RISING))
    lds(r16, ICR1L)
    st(X+, r16) ; 2 clks
    in(r16, PIND) ; 1 clk
    st(X+, r16) ; 2 clk

    cpi(XH, end_of_samples >> 8) ; 1 clk
    brne(LOC(NEXT_SAMPLE)) ; 2 clks if branch taken, 1 clk if staying here
    cpi(XL, end_of_samples & 0xff) ; 1 clk
    brne(LOC(NEXT_SAMPLE)) ; 2 clks if branch taken, 1 clk if staying here
    
    ; calculate the increments of the timestamps
    ldi(XL, samples & 0xff)
    ldi(XH, samples >> 8)
    clr(r16)
  LOC(NEXT_DIFF):
    ld(r17, X)
    mov(r18, r17)
    sub(r17, r16)
    st(X, r17)
    adiw(XL, 2)
    mov(r16, r18)

    cpi(XH, end_of_samples >> 8)
    brne(LOC(NEXT_DIFF))
    cpi(XL, end_of_samples & 0xff) 
    brne(LOC(NEXT_DIFF))

    ret()
ENDFUNC ; sampler_zcd


FUNCTION(sampler_fullspeed_scope)
    ; Read the next sweep of samples from ADC
    ldi(r16, NUM_SAMPLES / 4)
    mov(r2, r16)

    ldi(cmd_start_adc, _BV(ADEN, ADSC) | 2)
    ldi(r17, _BV(ADEN) | 1) ; fullspeed prescaler setting

    rcall(wait_trig)

  LOC(START_SAMPLING):
    cli()
    sts(ADCSRA, cmd_start_adc)

  LOC(NEXT_SAMPLE):

    rcall(LOC(WAIT))
    lds(r16, ADCH) ; 2 clk
    sts(ADCSRA, cmd_start_adc) ; 2 clk
    st(X+, r16) ; 2 clk
    in(r16, PIND) ; 1 clk
    st(X+, r16) ; 2 clk

    rcall(LOC(WAIT))
    ; here: 44 CPUclks after start, = 13 ADCclks
    lds(r16, ADCH) ; 2 clk
    ; here: 41 CPUclks after start, that is the total sampling time
    sts(ADCSRA, cmd_start_adc) ; 2 clk
    st(X+, r16) ; 2 clk
    in(r16, PIND) ; 1 clk
    st(X+, r16) ; 2 clk

    rcall(LOC(WAIT))
    lds(r16, ADCH) ; 2 clk
    sts(ADCSRA, cmd_start_adc) ; 2 clk
    st(X+, r16) ; 2 clk
    in(r16, PIND) ; 1 clk
    st(X+, r16) ; 2 clk

    rcall(LOC(WAIT))
    lds(r16, ADCH) ; 2 clk
    sts(ADCSRA, cmd_start_adc) ; 2 clk
    st(X+, r16) ; 2 clk
    in(r16, PIND) ; 1 clk
    st(X+, r16) ; 2 clk

    dec(r2) ; 1 clk
    brne(LOC(NEXT_SAMPLE)) ; 2 clks

    ret()

  LOC(WAIT):
  LOC(WAIT_5_BITS): ; rcall = 3 clks
    ; here: 8 CPUclks after start = 2 ADCclks
    nop2() ; 2 clks
    nop2() ; 2 clks
    ; here: 12 CPUclks after start = 3 ADCclks
    nop2() ; 2 clks
    nop2() ; 2 clks
    ; here: 16 CPUclks after start = 4 ADCclks
    nop2() ; 2 clks
    nop2() ; 2 clks
    ; here: 20 CPUclks after start = 5 ADCclks
    nop2() ; 2 clks
    nop2() ; 2 clks
    ; here: 24 CPUclks after start = 6 ADCclks
    nop2() ; 2 clks
    nop2() ; 2 clks
    ; here: 28 CPUclks after start = 7 ADCclks
    nop2() ; 2 clks
    nop2() ; 2 clks
    ; here: 32 CPUclks after start = 8 ADCclks
    sts(ADCSRA, r17) ; 2 clk, set to 1:2
    ; here: 34 CPUclks after start = 9 ADCclks
    nop2() ; 2 clks
    ; here: 36 CPUclks after start = 10 ADCclks
    nop2() ; 2 clks
    ; here: 38 CPUclks after start = 11 ADCclks
    ret() ; 4 clks = +2 ADCclks -> 13 after return
    
ENDFUNC ; sampler_fullspeed_scope


; ------------------------------------------------------------------------------

FUNCTION(main)
    ; Switch off unneeded subsystems
    ldi(r16, _BV(PRTWI, PRTIM0, PRSPI0))
    sts(PRR, r16)

    ; D3 is output (pwm), D2 is output (led), D[7,6,5,4] are inputs (digital)
    in(r16, DDRD)
    andi(r16, ~_BV(7,6,5,4))
    ori(r16, _BV(D_PWM, D_LED))
    out(DDRD, r16)
    
    ; C0 and C1 are inputs (adc)
    in(r16, DDRC)
    andi(r16, ~_BV(C_IN_DC, C_IN_AC))
    out(DDRC, r16)
    ; C0 and C1 are analogue inputs (adc)
    ldi(r16,  _BV(C_IN_DC, C_IN_AC))
    sts(DIDR0, r16)

    ; Set up Timer1 for Input Capture mode
    clr(r16)
    sts(TCCR1A, r16)
    ldi(r16, 3) ; 3=prescaler 1:64
    sts(TCCR1B, r16)
    ; captured data can be read from ICR1L / H
    ; on capture event ICF is set in TIFR1, must be manually cleared by *setting* it sbi(TIFR1, ICF)

    ; Set up Comparator for Input Capture mode
    ldi(r16, _BV(ACME))
    sts(ADCSRB, r16)
    ldi(r16, _BV(ACIC))
    out(ACSR, r16)

    ; Set up Timer2 for FastPWM mode
    ldi(r16, _BV(COM2B1, WGM21, WGM20))
    sts(TCCR2A, r16)
    ldi(r16, _BV(WGM22) | 3) ; FIXME: prescaler hardwired to 16M/32 = 500kHz
    sts(TCCR2B, r16)
    ldi(r16, 250 - 1)
    sts(OCR2A, r16) ; pwm wave length, freq = 500kHz / 250 = 2 kHz
    ldi(r16, 100) 
    sts(OCR2B, r16) ; pwm high state length, must be < wave length, now: 100/250 = 40%

    ; Set up ADC
    ldi(admux, _BV(ADLAR, REFS1, REFS0)) ; input=C0, left adjust, ref=AVcc -> 20 mV per unit
    sts(ADMUX, admux)
    ldi(r16, 0)
    rcall(set_sample_rate)

    ; Set up USART recv buffer pointer
    ldi(YL, recv_buf & 0xff)
    ldi(YH, recv_buf >> 8)
    ; Usart to USE_BAUD, 8 data, even pty, 1 stop, rx data irq
    ldi(r16, HI8(USE_BAUD))
    sts(UBRR0H, r16)
    ldi(r16, LO8(USE_BAUD))
    sts(UBRR0L, r16)
    ldi(r16, _BV(U2X0))
    sts(UCSR0A, r16)
    ldi(r16, _BV(UCSZ01, UCSZ00, UPM01)) ; 8bits, even pty, 1 stop
    sts(UCSR0C, r16)
    ldi(r16, _BV(RXEN0, TXEN0, RXCIE0, UDRIE0))
    sts(UCSR0B, r16) ; Enable USART

    ; Set up operation mode
    ldi(op_mode, OP_SCOPE)

    ; Set up sample buffer pointer
    ldi(XL, samples & 0xff)
    ldi(XH, samples >> 8)

    ; Set up trigger
    ldi(trig_type, TRIG_RISING_ANALOG)
    ldi(r16, 0x80)
    mov(trig_level, r16)

    sbi(GPIOR0, SEND_COMPLETE)
    sei()

    ; ------------------------------------------------------------------------------
  LOC(MAIN_LOOP):
    ; Set up sample buffer pointer
    ldi(XL, samples & 0x0f)
    ldi(XH, samples >> 8)

    cpi(op_mode, OP_SCOPE)
    brne(LOC(NOT_SCOPE))
    rcall(sampler_analog_scope)
    rjmp(LOC(SEND_SAMPLES))
  LOC(NOT_SCOPE):

    cpi(op_mode, OP_ZCD)
    brne(LOC(NOT_ZCD))
    rcall(sampler_zcd)
    rjmp(LOC(SEND_SAMPLES))
  LOC(NOT_ZCD):

    cpi(op_mode, OP_FULLSPEED_SCOPE)
    brne(LOC(NOT_FULLSPEED_SCOPE))
    rcall(sampler_fullspeed_scope)
    rjmp(LOC(SEND_SAMPLES))
  LOC(NOT_FULLSPEED_SCOPE):

  LOC(SEND_SAMPLES):
    ; Send the samples to the USART
    ldi(XL, samples & 0xff)
    ldi(XH, samples >> 8)
    cbi(GPIOR0, SEND_COMPLETE)
    rcall(USART_UDRE_vect) ; will enable interrupts on return
    
  LOC(WAIT_SEND_COMPLETE):
    ; USART_UDRE_vect will set SEND_COMPLETE flag in GPIOR0 when finished
    sbis(GPIOR0, SEND_COMPLETE)
    rjmp(LOC(WAIT_SEND_COMPLETE))

    rjmp(LOC(MAIN_LOOP))

ENDFUNC ; main


; ------------------------------------------------------------------------------

;    mov(ZL, r16)
;    clr(ZH)
;    addiw__ ZL, ZH, led_b210 << 1 
;       subi  r30, low(-(led_b210 << 1)) 
;	    sbci  r31, high(-(led_b210 << 1))
;    lpm(r16, Z)
;led_b210:   .DB 3, 0, 5, 5, 6, 7, 7, 0, 7, 7, 6, 7, 3, 5, 7, 6


; ------------------------------------------------------------------------------
.DSEG
recv_buf:           .byte 1*RECV_BUF_SIZE
samples:            .byte 2*NUM_SAMPLES
end_of_samples:

;{ vim: set sw=4 ts=4 ft=asm et: }
